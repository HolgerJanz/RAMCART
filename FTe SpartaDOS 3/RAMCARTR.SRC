;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; Restore RAMCART RAM to flash
;

;
; Main
;
; The!Cart 512k user space banks from $3fc0 - $3fff
; The!Cart 640k user space banks from $3fb0 - $3fef
;
        *= $3000
 
        jsr PRINT
        .byte "RESTORE ver60 for The!Ramdisk by hjx",$9B,$FF
        jsr globalStoreTCreg
 
; check if we are running on THE!CART hardware
        lda TCMOSL
        cmp #TCMOSLXLLOCK
        beq errorNoCart
        cmp #TCMOSLXELOCK
        beq errorNoCart
        bne startCopy
 
errorNoCart
        jsr globalRestoreTCreg
        jsr PRINT
        .byte "  Error - The!Cart not found",$9B,$FF
        rts
 
startCopy
        jsr PRINT
        .byte "  Start restore. Copy Flash to RAM.",$9b,$ff
; set flash ram bank
; 512k user space start bank $3fc0
; 640k user space start bank $3fb0
        lda #$b0
        sta flashBL
        lda #$3f
        sta flashBH        
_loopCopy
; read
        jsr storeTCreg
        jsr setTCFLASH
        jsr copy8k_A24
        jsr restoreTCreg
; write
        jsr storeTCreg
        jsr setTCRAM
        jsr copy8k_42A
        jsr restoreTCreg
; increase
        inc flashBL
        inc ramBank
        lda #$40
        cmp ramBank
        bne _loopCopy
; finished        
        jsr globalRestoreTCreg        
        jsr PRINT
        .byte "  End restore",$9B,$FF
        rts
        
;
; Subroutines
;
        .include "TCCOPY.INC"
